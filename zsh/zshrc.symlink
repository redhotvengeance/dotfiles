if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

export DOTFILES=$HOME/.dotfiles

source $DOTFILES/zsh/plugins.zsh

# Sources: https://github.com/nicknisi/dotfiles/blob/master/zsh/zshrc.symlink and https://github.com/holman/dotfiles/blob/master/zsh/zshrc.symlink

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# add dotfiles bin to the path
export PATH=$DOTFILES/bin:$PATH

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# check for and load fzf.zsh
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

if [ -z ${DOTFILES_LOADED+x} ]; then
  # Access zsh online help
  unalias run-help
  autoload run-help
  HELPDIR=/usr/local/share/zsh/help
fi

[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# check for and load local zshrc
[[ -f ~/.zshrc.local ]] && source ~/.zshrc.local

export DOTFILES_LOADED=1
