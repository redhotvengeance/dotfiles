export DOTFILES=$HOME/.dotfiles

export EDITOR=nvim
export GIT_EDITOR=nvim
export VISUAL=nvim

source $DOTFILES/zsh/plugins.zsh

# Sources: https://github.com/nicknisi/dotfiles/blob/master/zsh/zshrc.symlink and https://github.com/holman/dotfiles/blob/master/zsh/zshrc.symlink

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# add dotfiles bin to the path
export PATH=$DOTFILES/bin:$PATH

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

autoload edit-command-line
zle -N edit-command-line
bindkey -M vicmd "^v" edit-command-line

# check for and load fzf.zsh
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

eval "$(starship init zsh)"

# check for and load local zshrc
[[ -f ~/.zshrc.local ]] && source ~/.zshrc.local

export DOTFILES_LOADED=1
